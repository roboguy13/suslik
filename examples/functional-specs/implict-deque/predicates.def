// Following the decomposition of non-regular (aka non-uniform) types into
// regular types given in "Purely Functional Data Structures" by Okasaki (Sec. 10.1.1)

predicate implicit_deque(int sz, set s, loc x) {
| x == null => { sz == 0 && s =i {} ; emp }
| not (x == null) => {
    s =i s1 ++ s2 &&
    sz == bin_sz + sz1
      ;
    [x, 2] ** x :-> v ** (x+1) :-> nxt ** implicit_deque(sz1, s2, nxt)
    ** binary_tree(bin_sz, s1, v)
  }
}

predicate binary_tree(int sz, set s, loc x) {
| true => { x :-> tag ** binary_tree_inner(sz, s, tag, x) }
}

// Tag is stored at x
predicate binary_tree_inner(int sz, set s, int tag, loc x) {
| tag == 0 => { sz == 1 && s =i {v} ; [x, 2] ** (x+1) :-> v }
| tag == 1 =>
    {
      sz == 1 + sz1 + sz2 &&
      s =i s1 ++ s2
        ;
      [x, 3] ** (x+1) :-> left ** (x+2) :-> right ** binary_tree(sz1, s1, left) ** binary_tree(sz2, s2, right)
    }
}

predicate pair(loc x, loc y, loc z) {
| true => { [x, 2] ** x :-> y ** (x+1) :-> z }
}

predicate sll(set s, loc x) {
| x == null => { s =i {} ; emp }
| not (x == null) => { s =i {v} ++ s1 ; [x, 2] ** x :-> v ** (x+1) :-> nxt ** sll(s1, nxt) }
}




predicate lseg2(loc x, set s) {
|  x == null        => { s =i {} ; emp }
|  not (x == null)  => { s =i {v} ++ s1 ; [x, 3] ** x :-> v ** (x + 1) :-> (v + 1) ** (x + 2) :-> nxt ** lseg2(nxt, s1) }
}

predicate rose_tree(loc x, set s) {
|  x == 0        => { s =i {} ; emp }
|  not (x == 0)  => { s == {v} ++ s1 ; [x, 2] ** x :-> v ** (x + 1) :-> b ** buds(b, s1)}
}

predicate buds(loc x, set s) {
|  x == 0        => { s == {}; emp }
|  not (x == 0)  => { s == s1 ++ s2 ;
                      [x, 2] ** x :-> r ** rose_tree(r, s1) ** (x + 1) :-> nxt ** buds(nxt, s2) }
}

